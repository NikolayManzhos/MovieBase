apply plugin: 'com.android.application'

apply plugin: 'jacoco'
apply from: "$rootDir/jacoco.gradle"

def keystoreProperties = new Properties()
def keyFile = rootProject.file("keystore.properties")
if (!keyFile.exists()) {
    keystoreProperties.setProperty "MovieDbSecretKey", System.getenv("MovieDbSecretKey")
    keystoreProperties.setProperty "YoutubeSecretKey", System.getenv("YoutubeSecretKey")
} else {
    keystoreProperties.load(new FileInputStream(keyFile))
}

def isDebug = false
def isSigned = false

afterEvaluate {
    if (isDebug)  {
        assembleDebug.dependsOn 'testDebugUnitTest'
    } else  {
        assembleRelease.dependsOn 'testDebugUnitTest', 'lintRelease'
    }

    /*
        Verifies if sign.properties is exists
        If it does then set's signingConfig properties
     */
    def name = 'release'
    def signProperties = new Properties()
    def signFile = rootProject.file("sign.properties")
    def signingConfig = android.signingConfigs[name]
    if (signFile.exists()) {
        isSigned = true
        signProperties.load(new FileInputStream(signFile))
        signingConfig.keyAlias = signProperties['KeyAlias']
        signingConfig.keyPassword = signProperties['KeyPassword']
        signingConfig.storeFile = file(signProperties['StoreFile'])
        signingConfig.storePassword = signProperties['StorePassword']

        android.buildTypes.release.signingConfig = signingConfig
    }
}

configurations.getByName("androidTestCompile") {
    resolutionStrategy {
        force "com.android.support:appcompat-v7:$support"
    }
}

android {
    signingConfigs {
        def mock = "mb" //fake property, will be replaced at runtime
        release {
            storeFile file(mock)
            keyAlias mock
            storePassword mock
            keyPassword mock
        }
    }
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        applicationId "com.defaultapps.moviebase"
        testInstrumentationRunner 'com.defaultapps.moviebase.CustomTestRunner'
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            signingConfig null // will produce unsigned apk if no sign.properties present
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            isDebug = true
            debuggable true
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }
    buildTypes.each {
        it.buildConfigField 'String', 'MDB_API_KEY', keystoreProperties['MovieDbSecretKey']
        it.resValue 'string', 'YOUTUBE_API_KEY', keystoreProperties['YoutubeSecretKey']
        it.proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    sourceSets {
        main {
            def resPath = 'src/main/res'
            res.srcDirs =
                    [
                            "${resPath}/layouts/home",
                            "${resPath}/layouts/discover",
                            "${resPath}/layouts/bookmarks",
                            "${resPath}/layouts/search",
                            "${resPath}/layouts/genre",
                            "${resPath}/layouts/movie",
                            "${resPath}/layouts/custom_views",
                            "${resPath}/layouts/user",
                            "${resPath}/layouts/person",
                            "${resPath}/layouts/login",
                            "${resPath}/layouts",
                            "${resPath}"
                    ]
        }
        test.resources.srcDirs += 'src/main/assets'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$support"
    implementation "com.android.support:preference-v7:$support"
    implementation "com.android.support:design:$support"
    implementation "com.android.support:cardview-v7:$support"
    implementation "com.android.support:customtabs:$support"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayout"
    implementation "com.roughike:bottom-bar:$bottomBar"
    //ButterKnife
    implementation "com.jakewharton:butterknife:$butterKnife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterKnife"
    //Dagger 2
    implementation "com.google.dagger:dagger:$dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger"
    //RxJava 2
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroid"
    implementation "io.reactivex.rxjava2:rxjava:$rxjava"
    //Retrofit 2
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    //Firebase
    implementation "com.google.firebase:firebase-core:$firebase"
    implementation "com.firebaseui:firebase-ui-auth:$firebaseUi"
    implementation "com.firebaseui:firebase-ui-database:$firebaseUi"
    //LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary"
    //Timber
    implementation "com.jakewharton.timber:timber:$timber"
    //Prefs
    implementation "com.github.nikolaymanzhos:preferenceshelper:$rootProject.prefs"
    //Picasso
    implementation "com.squareup.picasso:picasso:$rootProject.picasso"
    //Iconify
    implementation "com.joanzapata.iconify:android-iconify-material:$rootProject.iconify"
    //Custom UI libraries
    implementation "de.hdodenhof:circleimageview:$circle"
    implementation "com.thefinestartist:ytpa:$youtube"
    implementation "com.yarolegovich:mp:$materialPref"
    implementation "com.miguelcatalan:materialsearchview:$materialSearch"
    implementation "com.ms-square:expandableTextView:$expTextView"


    testImplementation "junit:junit:$junit"
    testImplementation "org.mockito:mockito-core:$mockito"
    testImplementation "io.github.benas:random-beans:$random_beans"
    testImplementation "org.robolectric:robolectric:$robolectric"
    testImplementation "org.robolectric:shadows-support-v4:$robolectric_shadow"
    androidTestImplementation "com.android.support:appcompat-v7:$support"
    androidTestImplementation "com.android.support:design:$support"
    androidTestImplementation "com.android.support:cardview-v7:$support"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espresso"
    androidTestImplementation "org.mockito:mockito-core:$mockito"
}
apply plugin: 'com.google.gms.google-services'
