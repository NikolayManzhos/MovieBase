apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))

def isDebug = false;

afterEvaluate {
    if (isDebug)  {
        assembleDebug.dependsOn 'test'
    } else  {
        assembleRelease.dependsOn 'test', 'lint', 'connectedCheck'
    }
}

configurations.getByName("androidTestCompile") {
    resolutionStrategy {
        force "com.android.support:appcompat-v7:$rootProject.support"
    }
}

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        applicationId "com.defaultapps.moviebase"
        testInstrumentationRunner 'com.defaultapps.moviebase.CustomTestRunner'
        minSdkVersion 17
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            isDebug = true
            debuggable true
            minifyEnabled false
            shrinkResources false
            multiDexEnabled true
            testCoverageEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

    }
    buildTypes.each {
        it.buildConfigField 'String', 'MDB_API_KEY', keystoreProperties['MovieDbSecretKey']
        it.resValue 'string', 'YOUTUBE_API_KEY', keystoreProperties['YoutubeSecretKey']
        it.proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }

    sourceSets {
        main {
            def resPath = 'src/main/res'
            res.srcDirs =
                    [
                            "${resPath}/layouts/home",
                            "${resPath}/layouts/discover",
                            "${resPath}/layouts/bookmarks",
                            "${resPath}/layouts/search",
                            "${resPath}/layouts/genre",
                            "${resPath}/layouts/movie",
                            "${resPath}/layouts/custom_views",
                            "${resPath}/layouts/user",
                            "${resPath}/layouts/person",
                            "${resPath}/layouts",
                            "${resPath}"
                    ]
        }
        test.resources.srcDirs += 'src/main/assets'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$rootProject.support"
    compile "com.android.support:preference-v7:$rootProject.support"
    compile "com.android.support:design:$rootProject.support"
    compile "com.android.support:cardview-v7:$rootProject.support"
    compile "com.android.support:customtabs:$rootProject.support"
    compile "com.android.support.constraint:constraint-layout:$constraintLayout"
    compile "com.roughike:bottom-bar:$rootProject.bottomBar"
    //ButterKnife
    compile "com.jakewharton:butterknife:$rootProject.butterKnife"
    annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.butterKnife"
    //Dagger 2
    compile "com.google.dagger:dagger:$rootProject.dagger"
    annotationProcessor "com.google.dagger:dagger-compiler:$rootProject.dagger"
    //RxJava 2
    compile "io.reactivex.rxjava2:rxandroid:$rootProject.rxandroid"
    compile "io.reactivex.rxjava2:rxjava:$rootProject.rxjava"
    //Retrofit 2
    compile "com.squareup.retrofit2:retrofit:$rootProject.retrofit"
    compile "com.squareup.retrofit2:converter-gson:$rootProject.retrofit"
    compile "com.squareup.retrofit2:adapter-rxjava2:$rootProject.retrofit"
    compile "com.squareup.okhttp3:logging-interceptor:$rootProject.okhttpVersion"
    //Firebase
    compile "com.google.firebase:firebase-core:$rootProject.firebase"
    compile "com.firebaseui:firebase-ui-auth:$rootProject.firebaseUi"
    compile "com.firebaseui:firebase-ui-database:$rootProject.firebaseUi"
    //LeakCanary
    debugCompile "com.squareup.leakcanary:leakcanary-android:$rootProject.leakcanary"
    testCompile "com.squareup.leakcanary:leakcanary-android-no-op:$rootProject.leakcanary"
    //Prefs
    compile "com.github.nikolaymanzhos:preferenceshelper:$rootProject.prefs"
    //Picasso
    compile "com.squareup.picasso:picasso:$rootProject.picasso"
    //Iconify
    compile "com.joanzapata.iconify:android-iconify-material:$rootProject.iconify"
    //Custom UI libraries
    compile "de.hdodenhof:circleimageview:$rootProject.circle"
    compile "com.thefinestartist:ytpa:$rootProject.youtube"
    compile "com.yarolegovich:mp:$rootProject.materialPref"
    compile "com.miguelcatalan:materialsearchview:$rootProject.materialSearch"
    compile "com.ms-square:expandableTextView:$expTextView"



    testCompile "junit:junit:$rootProject.junit"
    testCompile "org.mockito:mockito-core:$rootProject.mockito"
    testCompile "org.powermock:powermock-module-junit4:$rootProject.powerMockito"
    testCompile "org.powermock:powermock-api-mockito2:$rootProject.powerMockito"
    testCompile "org.robolectric:robolectric:$rootProject.robolectric"
    testCompile "org.robolectric:shadows-support-v4:$rootProject.robolectric"
    androidTestCompile "com.android.support:appcompat-v7:$rootProject.support"
    androidTestCompile "com.android.support:design:$rootProject.support"
    androidTestCompile "com.android.support:cardview-v7:$rootProject.support"
    androidTestCompile("com.android.support.test.espresso:espresso-core:$rootProject.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestCompile "com.android.support.test.espresso:espresso-contrib:$espresso"
    androidTestCompile "org.mockito:mockito-core:$rootProject.mockito"
}
apply plugin: 'com.google.gms.google-services'
